//
//

{
	var A = require('./ast');

	function hat(head, tail, ix) {
		return [head].concat(tail.map(function(t) { return t[ix]; }));
	}

	function makeBinaryOperator(head, rest) {
        var result = head;
        rest.forEach(function(i) {
            if (i[0] === '||') {
            	result = A.logicalOrExp(result, i[2]);
            } else if (i[0] === '&&') {
                result = A.logicalAndExp(result, i[2]);
            } else {
                result = A.binOpExp(result, i[0], i[2]);
            }
        });
        return result;
    }

	var npStack = [true];

	function pushNP() {
		npStack.push(true);
		return true;
	}

	function popNP() {
		npStack.pop();
		return true;
	}

	function canNP() {
		return npStack[npStack.length-1];
	}

	function enterNP() {
		if (!canNP()) return false;
		npStack[npStack.length-1] = false;
		return true;
	}

	function exitNP() {
		npStack[npStack.length-1] = true;
		return true;
	}
}

Start
	= Module

//
// Tokens

NL 				= '\n' / '\r' '\n'?

LE				= '<='
GE				= '>='
LSHIFT			= '<<'
RSHIFT			= '>>'
EQ				= '='
NEQ 			= '!='
MISSING_ARG		= '?'
POW 			= '**'
LOGICAL_OR		= '||'
LOGICAL_AND		= '&&'
BITWISE_OR		= '|' !'|'
BITWISE_AND		= '&' !'&'
NO_CALL 		= BITWISE_AND
BITWISE_NOT		= '~'
BITWISE_XOR 	= '^'
LAMBDA_START	= '.['
DOT 			= '.' !'['
COMMA			= ','
SEMI			= ';'
ASSIGN 			= ':='
MINUS			= '-'
PLUS			= '+'
TIMES			= '*' !'*'
DIVIDE			= '/'
MOD				= '%'
BANG			= '!' ![=]
LT 				= '<' ![=<]
GT				= '>' ![=>]
LBRACE			= '{'
RBRACE			= '}'
LBRACKET		= '['
RBRACKET		= ']'
LPAREN			= '('
RPAREN			= ')'
GLOBAL 			= '$' !ident_start

//
// Keywords

DEF				= 'def'			!ident_rest
ELSE			= 'else' 		!ident_rest
EVAL			= 'eval'		!ident_rest
EXPORT			= 'export'		!ident_rest
EXPORT_B		= 'export!'
FALSE			= 'false'		!ident_rest
FOR				= 'for'			!ident_rest
IF				= 'if' 			!ident_rest
IMPORT			= 'import'		!ident_rest
IMPORT_B		= 'import!'
IN 				= 'in'			!ident_rest
LOOP			= 'loop'		!ident_rest
MY				= 'my'			!ident_rest
NULL			= 'null'		!ident_rest
RETURN			= 'return' 		!ident_rest
SPAWN			= 'spawn' 		!ident_rest
TO 				= 'to'			!ident_rest
TRACE			= 'trace'		!ident_rest
TRUE			= 'true'		!ident_rest
WAIT			= 'wait'		!ident_rest
WHILE			= 'while'		!ident_rest
YIELD			= 'yield'		!ident_rest

Keyword
	= DEF
	/ ELSE
	/ EVAL
	/ EXPORT
	/ EXPORT_B
	/ FALSE
	/ FOR
	/ IF
	/ IMPORT
	/ IMPORT_B
	/ IN
	/ LOOP
	/ MY
	/ NULL
	/ RETURN
	/ SPAWN
	/ TO
	/ TRACE
	/ TRUE
	/ WAIT
	/ WHILE
	/ YIELD

//
// Helpers

_
	= [ \t]*

__
	= [ \t\r\n]*

// this denotes the whitespace permitted between a block statement's
// expression and subsequent brace
_b
	= __

// any characters that can be skipped including empty statements
_s
	= [ \t\r\n;]*

ident_start
	= [a-zA-Z]

ident_rest
	= [a-z0-9_]

ident
	= !Keyword name:$( ident_start ident_rest* ) { return name; }

method_name
	= $( ident method_name_modifier? )

method_name_modifier
	= '!'
	/ '?'

non_zero_digit
	= [1-9]

digit
	= [0-9]

integer
	= $( non_zero_digit digit* )
	/ $( '0' ) !digit !'x' !'b'

float
	= $( integer '.' digit+ )

hex
	= $( '0x' [0-9a-f]i+ )

binary
	= $( '0b' [01]+ )

//
// Expressions

UnaryOperator
    = PLUS
    / MINUS
    / BITWISE_NOT
    / BANG
    / NO_CALL

MultiplicativeOperator
    = TIMES
    / DIVIDE
    / MOD
    / POW

AdditiveOperator
    = PLUS
    / MINUS

ShiftOperator
    = LSHIFT
    / RSHIFT

RelationalOperator
    = LE
    / GE
    / LT
    / GT

EqualityOperator
    = EQ
    / NEQ

BitwiseAndOperator
    = BITWISE_AND

BitwiseXorOperator
    = BITWISE_XOR

BitwiseOrOperator
    = BITWISE_OR

LogicalAndOperator
    = LOGICAL_AND

LogicalOrOperator
    = LOGICAL_OR

/* Expression */

Expression
    = AssignmentExpression

AssignmentExpression
    = l:LeftHandSideExpression _ ASSIGN _ r:AssignmentExpression {
    	return A.assignmentExp(l, r);
    }
    / LogicalOrExpression

LogicalOrExpression
    = head:LogicalAndExpression rest:( LogicalOrOperator _ LogicalAndExpression )* {
        return makeBinaryOperator(head, rest);
    }

LogicalAndExpression
    = head:BitwiseOrExpression rest:( LogicalAndOperator _ BitwiseOrExpression )* {
        return makeBinaryOperator(head, rest);
    }

BitwiseOrExpression
    = head:BitwiseXorExpression rest: ( BitwiseOrOperator _ BitwiseXorExpression )* {
        return makeBinaryOperator(head, rest);
    }

BitwiseXorExpression
    = head:BitwiseAndExpression rest: ( BitwiseXorOperator _ BitwiseAndExpression )* {
        return makeBinaryOperator(head, rest);
    }

BitwiseAndExpression
    = head:EqualityExpression rest: ( BitwiseAndOperator _ EqualityExpression )* {
        return makeBinaryOperator(head, rest);
    }

EqualityExpression
    = head:RelationalExpression rest: ( EqualityOperator _ RelationalExpression )* {
        return makeBinaryOperator(head, rest);
    }

RelationalExpression
    = head:ShiftExpression rest: ( RelationalOperator _ ShiftExpression )* {
        return makeBinaryOperator(head, rest);
    }

ShiftExpression
    = head:AdditiveExpression rest: ( ShiftOperator _ AdditiveExpression )* {
        return makeBinaryOperator(head, rest);
    }

AdditiveExpression
    = head:MultiplicativeExpression rest: ( AdditiveOperator _ MultiplicativeExpression )* {
        return makeBinaryOperator(head, rest);
    }

MultiplicativeExpression
    = head:UnaryExpression rest: ( MultiplicativeOperator _ UnaryExpression )* {
        return makeBinaryOperator(head, rest);
    }

UnaryExpression
    = LeftHandSideExpression
    / op:UnaryOperator _ exp:UnaryExpression {
        return A.unaryOpExp(op, exp);
    }

LeftHandSideExpression
    = CallExpression
    / CallNoParenExpression
    / MemberExpression

CallExpression
    = spawn:SPAWN? _ callee:MemberExpression args:Arguments rest:(CallCons)* {
    	var res = A.call(callee, args);
    	rest.forEach(function(r) {
    		if (Array.isArray(r)) {
    			res = A.call(res, r);
    		} else if (r.computed) {
    			res = A.computedMemberExp(res, r.property);
    		} else {
    			res = A.staticMemberExp(res, r.property);
    		}
    	});
    	if (spawn) {
    		res = A.spawn(res);
    	}
    	return res;
    }

CallNoParenExpression
	= SPAWN? _ callee:MemberExpression
		&CNPStartToken
		&{ return enterNP(); }
		args:ArgumentList
		&{ return exitNP(); } {
		return A.call(callee, args);
	}

// An exhaustive list of valid tokens that can appear at the start
// of a function call with no parens
CNPStartToken
	= MISSING_ARG
	/ BITWISE_NOT
	/ LAMBDA_START
	/ BANG
	/ LBRACE
	/ GLOBAL
	/ TRUE
	/ FALSE
	/ TRACE
	/ NumericLiteral
	/ StringLiteral
	/ ColorLiteral
	/ Ident
	/ GlobalIdent

Arguments
	= '(' _ ')' _ { return []; }
    / '(' &{ return pushNP(); } _ args:ArgumentList ')' &{ return popNP(); } _ { return args; }
    
ArgumentList
    = head:Argument rest:( ',' _ Argument )* { return hat(head, rest, 2); }

Argument
	= Expression
	/ MISSING_ARG _ { return A.missingArgument(); }

CallCons
    = Arguments
    / Member

MemberExpression
    = first:Primary rest:Member* {
        var result = first;
        rest.forEach(function(r) {
            if (r.computed) {
                result = A.computedMemberExp(result, r.property);
            } else {
                result = A.staticMemberExp(result, r.property);
            }
        });
        return result;
    }

Member
    = ComputedMember
    / StaticMember

ComputedMember
    = '[' _ property:Expression ']' _ {
    	return { computed: true, property: property };
    }

StaticMember
    = '.' _ property:ident _ {
        return { computed: false, property: property };
    }

//
// Atoms

Primary
	= Atom
	/ Lambda

Atom
	= v:Constant _ { return v; }
	/ v:Ident _ { return v; }
	/ v:GlobalObject _ { return v; }
	/ v:GlobalIdent _ { return v; }
	/ v:ArrayLiteral _ { return v; }
	/ v:DictionaryLiteral _ { return v; }
	/ '(' &{ return pushNP(); } _ exp:Expression _ ')' &{ return popNP(); } _ {
		return exp;
	}

Constant
	= NumericLiteral
	/ v:StringLiteral _ { return v; }
	/ v:ColorLiteral _ { return v; }
	/ v:BooleanLiteral _ { return v; }

NumericLiteral
	= v:FloatLiteral _ { return v; }
	/ v:IntegerLiteral _ { return v; }
	/ v:HexLiteral _ { return v; }
	/ v:BinaryLiteral _ { return v; }

IntegerLiteral
	= v:integer {
		return parseInt(v, 10);
	}

HexLiteral
	= v:hex {
		return parseInt(v.substr(2), 16);
	}

BinaryLiteral
	= v:binary {
		return parseInt(v.substr(2), 2);
	}

FloatLiteral
	= v:float {
		return parseFloat(v);
	}

StringLiteral
	= DoubleQuotedString

DoubleQuotedString
	= '"' str:(DoubleQuotedStringChar*) '"' {
		return str.join('');
	}

DoubleQuotedStringChar
	= "\\n"     { return "\n"; }
	/ "\\r"     { return "\r"; }
	/ "\\t"     { return "\t"; }
	/ "\\'"     { return "'"; }
	/ '\\"'     { return '"'; }
	/ '\\\\'    { return "\\"; }
	/ $ ( !["\\] . )

ColorLiteral
	= HexColorLiteral
	/ NamedColorLiteral

HexColorLiteral
	= '#' v:$( HCD HCD HCD HCD HCD HCD HCD HCD ) {
		return A.hexColor(v);
	}
	/ '#' v:$( HCD HCD HCD HCD HCD HCD ) {
		return A.hexColor('FF' + v);
	}
	
HCD
	= [0-9A-Fa-f]

NamedColorLiteral
	= '#' name:$( [a-z]i+ ) {
		return A.namedColor(name);
	}

BooleanLiteral
	= TRUE { return true; }
	/ FALSE { return false; }

Ident
	= ident:$( ident ) {
		return A.ident(ident);
	}

GlobalObject
	= GLOBAL {
		return A.globalObject();
	}

GlobalIdent
	= '$' ident:ident {
		return A.globalIdent(ident);
	}

ArrayLiteral
	= '[' _ items:ArrayLiteralInner? ']' _ {
		return A.arrayLiteral(items || []);
	}

ArrayLiteralInner
	= head:Expression _ tail:(',' _ Expression)* { return hat(head, tail, 2); }

DictionaryLiteral
	= '{' _ pairs:DictionaryLiteralInner? '}' _ {
		return A.dictionaryLiteral(pairs || []);
	}

DictionaryLiteralInner
	= head:DictionaryPair _ tail:(',' _ DictionaryPair)* { return hat(head, tail, 2); }

DictionaryPair
	= key:DictionaryKey _ ':' _ exp:Expression {
		return A.dictionaryPair(key, exp);
	}

DictionaryKey
	= Ident
	/ StringLiteral

//
//

Lambda
	= LAMBDA_START _ args:LambdaArgs? &{ return pushNP(); } body:Statements RBRACKET &{ return popNP(); } {
		return A.lambda(args || [], body);
	}

LambdaArgs
	= head:ident _ tail:(',' _ ident _)* '|' {
		return [ A.requiredParameter(head) ].concat(tail.map(function(i) {
			return A.requiredParameter(i[2]);
		}));
	}

//
// Productions

Module
	= __ ports:Ports stmts:Statements {
		return A.module(ports, stmts);
	}

Ports
	= ports:(Port NL)* {
		return ports.map(function(p) { return p[0]; });
	}

Port
	= Import
	/ ImportBang
	/ Export
	/ ExportBang

Import
	= IMPORT _ ident _ ImportSpecifics? PortAlias? {

	}

ImportBang
	= IMPORT_B _ ident _ ImportSpecifics? {

	}

ImportSpecifics
	= DOT _ LBRACE _ PortList RBRACE _ {

	}

Export
	= EXPORT _ PortList {

	}

ExportBang
	= EXPORT_B _ ident {

	}

PortList
	= head:(PortItem) tail:(',' _ PortItem)* {

	}

PortItem
	= ident _ PortAlias? {

	}

PortAlias
	= 'as' _ ident _ {

	}

Statements
	= _s stmts:Statement* { return stmts; }

Statement
	= stmt:BlockStatement								{ return stmt; }
	/ stmt:InlineStatement InlineStatementTerminator 	{ return stmt; }

Block
	= '{' stmts:Statements '}' _s 						{ return stmts; }

BlockStatement
	= WhileStatement
	/ LoopWhileStatement
	/ LoopStatement
	/ ForStatement
	/ ForeachStatement
	/ IfStatement
	/ FunctionDefinition

WhileStatement
	= WHILE _ exp:Expression _b body:Block {
		return A.whileStatement(exp, body);
	}

LoopWhileStatement
	= LOOP _ WHILE _ exp:Expression _b body:Block {
		return A.loopWhileStatement(exp, body);
	}

LoopStatement
	= LOOP _b body:Block {
		return A.loopStatement(body);
	}

ForStatement
	= FOR _ subject:ident _ IN _ start:Expression _ TO _ end:Expression _ step:('step' _ Expression)? _b body:Block {
		return A.forStatement(subject, start, end, step ? step[2] : null, body);
	}

ForeachStatement
	= FOR _ subject1:ident _ subject2:(',' _ ident _)? IN _ exp:Expression _b body:Block {
		return A.foreachStatement(subject1, subject2 ? subject2[2] : null, exp, body);
	}

IfStatement
	= IF _ exp:Expression _b body:Block clauses:ElseIfBlock* elseClause:ElseBlock? {
		var initialClause = A.ifClause(exp, body);
		var clauses = [initialClause].concat(clauses);
		if (elseClause) {
			clauses.push(elseClause);
		}
		return A.ifStatement(clauses);
	}

ElseIfBlock
	= _b ELSE _ IF _ exp:Expression _b body:Block {
		return A.ifClause(exp, body);
	}

ElseBlock
	= _b ELSE _b body:Block {
		return A.ifClause(null, body);
	}

FunctionDefinition
	= DEF _ name:method_name _ params:FunctionParameters? _b body:Block {
		return A.functionDefinition(name, params || [], body);
	}

FunctionParameters
	= '(' _ params:FunctionParameterList? ')' {
		return params || [];
	}

FunctionParameterList
	= p1:FunctionRequiredParameters p2:(',' _ FunctionOptionalParameters)? {
		return p2 ? p1.concat(p2[2]) : p1;
	}
	/ p1:FunctionOptionalParameters {
		return p1;
	}

FunctionRequiredParameters	= h:FRP t:(',' _ FRP)* 				{ return hat(h, t, 2); }
FunctionOptionalParameters	= h:FOP t:(',' _ FOP)* 				{ return hat(h, t, 2); }
					FRP 	= name:ident _ !'='					{ return A.requiredParameter(name); }
					FOP 	= name:ident _ '=' _ value:FOPExpr	{ return A.optionalParameter(name, value); }

FOPExpr
	= Constant

InlineStatement
	= ReturnStatement
	/ YieldStatement
	/ LocalVariables
	/ ExpressionStatement

// Inline statement ends with either a newline,
// semicolon, or if the end of the enclosing
// block/lambda is encountered.
InlineStatementTerminator
	= NL __
	/ SEMI __
	/ &'}'
	/ &']'

ReturnStatement
	= RETURN _ exp:Expression _ 	{ return A.returnStatement(exp); }
	/ RETURN _ 						{ return A.returnStatement(null); }

YieldStatement
	= YIELD _ 						{ return A.yieldStatement(); }

LocalVariables
	= MY head:LocalVariable tail:(',' _ LocalVariable)* {
		return A.localVariables(head.concat(tail.map(function(t) {
			return t[2];
		})));
	}

LocalVariable
	= name:Ident _ '=' _ exp:Expression _ 	{ return A.localVariable(name, exp); }
	/ name:Ident _ 							{ return A.localVariable(name, null); }

ExpressionStatement
	= exp:Expression _ 						{ return exp; }
